# Copyright (c) 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Sets up the dynamic library search path to include our "lib" directory.
config("executable_ldconfig") {
  ldflags = [
    # Want to pass "\$". Need to escape both '\' and '$'. GN will re-escape as
    # required for ninja.
    "-Wl,-rpath=\\\$ORIGIN/lib/",

    "-Wl,-rpath-link=lib/",
  ]
}

# This script returns a list consisting of two nested lists: the first is the
# list of cflags, the second are the linker flags.
pkg_script = "pkg-config.py"

config("fontconfig") {
  libs = [ "fontconfig" ]
}

config("freetype2") {
  pkgresult = exec_script(pkg_script, [ "freetype2" ], "value")
  include_dirs = pkgresult[0]
  cflags = pkgresult[1]
  libs = pkgresult[2]
  lib_dirs = pkgresult[3]
}

config("glib") {
  pkgresult = exec_script(pkg_script,
    [ "glib-2.0", "gmodule-2.0", "gobject-2.0", "gthread-2.0" ], "value" )
  include_dirs = pkgresult[0]
  cflags = pkgresult[1]
  libs = pkgresult[2]
  lib_dirs = pkgresult[3]
}

config("gtk") {
  # Gtk requires gmodule, but it does not list it as a dependency in some
  # misconfigured systems.
  pkgresult = exec_script(pkg_script,
    [ "gmodule-2.0", "gtk+-2.0", "gthread-2.0" ], "value" )
  include_dirs = pkgresult[0]
  cflags = pkgresult[1]
  libs = pkgresult[2]
  lib_dirs = pkgresult[3]

  defines = [ "TOOLKIT_GTK" ]
}

config("pangocairo") {
  pkgresult = exec_script(pkg_script, [ "pangocairo" ], "value" )
  include_dirs = pkgresult[0]
  cflags = pkgresult[1]
  libs = pkgresult[2]
  lib_dirs = pkgresult[3]
}

config("udev") {
  pkgresult = exec_script(pkg_script, [ "libudev" ], "value" )
  include_dirs = pkgresult[0]
  cflags = pkgresult[1]
  libs = pkgresult[2]
  lib_dirs = pkgresult[3]
}

config("x11") {
  # Don't bother running pkg-config for these X related libraries since it just
  # returns the same libs, and forking pkg-config is slow.
  defines = [ "USE_X11" ]
  libs = [
    "X11",
    "Xcomposite",
    "Xcursor",
    "Xdamage",
    "Xext",
    "Xfixes",
    "Xi",
    "Xrender",
    "Xss",
    "Xtst",
  ]
}
